//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.liyze09'
version = '1.3'

repositories {
    maven {
        url "https://maven.aliyun.com/repository/public"
    }
    maven {
        url "https://maven.aliyun.com/repository/gradle-plugin"
    }
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly 'org.jetbrains:annotations:24.0.1'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation 'org.slf4j:slf4j-api:2.0.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.4.7'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // https://mvnrepository.com/artifact/org.smartboot.http/smart-http-server
    implementation 'org.smartboot.http:smart-http-server:1.2.1'
    // https://mvnrepository.com/artifact/com.moandjiezana.toml/toml4j
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation 'com.google.code.gson:gson:2.10.1'
    // https://mvnrepository.com/artifact/org.smartboot.socket/aio-pro
    implementation 'org.smartboot.socket:aio-pro:1.5.29'
    // https://mvnrepository.com/artifact/net.bytebuddy/byte-buddy
    implementation 'net.bytebuddy:byte-buddy:1.14.5'
    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation 'com.zaxxer:HikariCP:5.0.1'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'
    // https://mvnrepository.com/artifact/de.ruedigermoeller/fst
    implementation 'de.ruedigermoeller:fst:2.57'
}
test {
    useJUnitPlatform()
}
sourceSets{
    java
}
jar{
    from("LICENSE")
    manifest {
        attributes 'Manifest-Version': '1.0'
        attributes 'Main-Class': 'net.liyze.basin.core.Main'
        attributes 'Class-Path': './data/jars'
        attributes 'Created-By': 'Gradle 8'
        attributes 'Built-By': 'Liyze09'
        attributes 'Build-Jdk': Runtime.version().toString()
        attributes 'Export-Command': 'net.liyze.basin.core.InternalPluginDemo'
        attributes 'Boot-Class': 'net.liyze.basin.core.InternalPluginDemo'
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/versions/**'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/INDEX.LIST'
    exclude 'module-info.class'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'Basin'
            pom {
                name = 'basin'
                description = 'Basin'
                url = 'hjttps://github.com/Liyze09/Basin'
                licenses {
                    license {
                        name = 'GPLv3'
                        url = ''
                    }
                }
                developers {
                    developer {
                        id = 'Liyze09'
                        name = 'Yuze Li'
                        email = 'a52990419@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Liyze09/Basin.git'
                    developerConnection = 'scm:git:ssh://github.com/Liyze09/Basin.git'
                    url = 'https://github.com/Liyze09/Basin'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("OSSRH_USERNAME") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("OSSEH_PASSWORD") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}
signing {
    sign(publishing.publications.mavenJava)
}
