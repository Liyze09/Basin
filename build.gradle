//file:noinspection GroovyAssignabilityCheck
//file:noinspection DependencyNotationArgument
plugins {
    id 'maven-publish'
    id 'signing'
    id 'java-library'
}

group = 'io.github.liyze09'
version = '1.4'

repositories {
    maven {
        url "https://maven.aliyun.com/repository/public"
    }
    maven {
        url "https://maven.aliyun.com/repository/gradle-plugin"
    }
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly 'org.jetbrains:annotations:24.0.1'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    api 'org.slf4j:slf4j-api:2.0.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    runtimeOnly 'ch.qos.logback:logback-classic:1.4.7'
    // https://mvnrepository.com/artifact/com.google.guava/guava
    api 'com.google.guava:guava:32.0.1-jre'
    // https://mvnrepository.com/artifact/org.smartboot.http/smart-http-server
    api 'org.smartboot.http:smart-http-server:1.2.1'
    // https://mvnrepository.com/artifact/com.moandjiezana.toml/toml4j
    api 'com.moandjiezana.toml:toml4j:0.7.2'
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    api 'com.google.code.gson:gson:2.10.1'
    // https://mvnrepository.com/artifact/org.smartboot.socket/aio-pro
    api 'org.smartboot.socket:aio-pro:1.5.29'
    // https://mvnrepository.com/artifact/net.bytebuddy/byte-buddy
    api 'net.bytebuddy:byte-buddy:1.14.5'
    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    api 'com.zaxxer:HikariCP:5.0.1'
}
test {
    useJUnitPlatform()
}
sourceSets{
    java
}
jar{
    from("LICENSE")
    manifest {
        attributes 'Manifest-Version': '1.0'
        attributes 'Main-Class': 'net.liyze.basin.core.Main'
        attributes 'Class-Path': './data/jars'
        attributes 'Created-By': 'Gradle 8'
        attributes 'Built-By': 'Liyze09'
        attributes 'Build-Jdk': Runtime.version().toString()
        attributes 'Export-Command': 'net.liyze.basin.core.InternalPluginDemo'
        attributes 'Boot-Class': 'net.liyze.basin.core.InternalPluginDemo'
    }
    if (System.getenv("CI") == null) {
        from {
            configurations.runtimeClasspath.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude 'META-INF/versions/**'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'module-info.class'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
javadoc {
    failOnError false
}
java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'Basin'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
            }
            pom {
                name = 'basin'
                description = 'Basin'
                url = 'https://github.com/Liyze09/Basin'
                licenses {
                    license {
                        name = 'GPLv3'
                        url = ''
                    }
                }
                developers {
                    developer {
                        id = 'Liyze09'
                        name = 'Yuze Li'
                        email = 'a52990419@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Liyze09/Basin.git'
                    developerConnection = 'scm:git:ssh://github.com/Liyze09/Basin.git'
                    url = 'https://github.com/Liyze09/Basin'
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("OSSRH_USERNAME") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("OSSEH_PASSWORD") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}
signing {
    sign(publishing.publications.maven)
}
